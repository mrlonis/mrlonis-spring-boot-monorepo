import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id "java"
    id "idea"
    id "jacoco"
    id "org.springframework.boot"
    id "io.spring.dependency-management"
    id "io.freefair.lombok"
    id "com.diffplug.spotless"
}

group = "com.mrlonis"
version = "0.0.1-SNAPSHOT"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2024.0.0"
    }
}

jacoco {
    // https://mvnrepository.com/artifact/org.jacoco/jacoco-maven-plugin
    toolVersion = "0.8.12"
}

tasks.withType(JavaCompile).configureEach {
    options.warnings = true
    options.deprecation = true
    options.compilerArgs << "-Xlint:unchecked"
    dependsOn("spotlessApply")
}

tasks.register('restartDocker') {
    doLast {
        exec {
            if (DefaultNativePlatform.currentOperatingSystem.isWindows()) {
                workingDir = file("./")
                commandLine = ["cmd", "/C", "docker compose", "up", "--pull", "always", "-V", "--wait"]
            } else {
                commandLine = ["docker", "compose", "up", "--pull", "always", "-V", "--wait"]
            }
        }
    }
}

spotless {
    java {
        cleanthat().version('2.20')
                .sourceCompatibility('21')
                .addMutator('SafeAndConsensual')
                .addMutator('SafeButNotConsensual')
        palantirJavaFormat('2.50.0').style('PALANTIR').formatJavadoc(true)
        formatAnnotations()
        removeUnusedImports()
        importOrder()
        trimTrailingWhitespace()
        endWithNewline()
    }
    groovyGradle {
        target '*.gradle' // default target of groovyGradle
        greclipse().configFile('../greclipse.properties')
        importOrder()
        trimTrailingWhitespace()
        endWithNewline()
    }
    flexmark {
        target '**/*.md'
        flexmark()
    }
}
